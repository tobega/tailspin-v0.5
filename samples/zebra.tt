-- WIP: Array content sequence matchers not yet implemented
-- Solution of https://rosettacode.org/wiki/Zebra_puzzle

permutations templates
  when <|=1> do [1] !
  otherwise
    n is $;
    expand templates
      p is $;
      1..$n -> [$p(..$-1)..., $n, $p($..)...] !
    end expand
    $n - 1 -> # -> expand !
end permutations

-- 1. There are five houses.
permutationsOf5 is [5 -> permutations];

nationalities is ['Englishman', 'Swede', 'Dane', 'Norwegian', 'German'];
colours is ['red', 'green', 'white', 'yellow', 'blue'];
pets is ['dog', 'bird', 'cat', 'horse', 'zebra'];
drinks is ['tea', 'coffee', 'milk', 'beer', 'water'];
smokes is ['Pall Mall', 'Dunhill', 'Blend', 'Blue Master', 'Prince'];

-- start with all permutations of colours
$permutationsOf5... -> $colours($) -> [$... -> {colour: $}]
  -- 5. The green house is immediately to the left of the white house.
  -> if <|[(<|{colour: <|='green'>}>:<|{colour: <|='white'>}>)]>
  -- zip with all permutations of nationalities
  -> templates
       current is $;
       $permutationsOf5... -> $nationalities($) -> $(.. as i; -> {$current($i), nationality: $}) !
     end
  -- 10. The Norwegian lives in the first house.
  -> if <|?($(1) matches <|{nationality: <|='Norwegian'>}>)>
  -- 2. The English man lives in the red house.
  -> if <|[<|{nationality: <|='Englishman'>, colour: <|='red'>}>]>
  -- 15. The Norwegian lives next to the blue house.
  -> if <|[(<|{nationality: <|='Norwegian'>}>:<|{colour: <|='blue'>}>)] | [(<|{colour: <|='blue'>}>:<|{nationality: <|='Norwegian'>}>)]>
  -- zip with all permutations of drinks
  -> templates
       current is $;
       $permutationsOf5... -> $drinks($) -> $(.. as i; -> {$current($i), drink: $}) !
     end
  -- 9. In the middle house they drink milk.
  -> if <|?($(3) matches <|{drink: <|='milk'>}>)>
  -- 6. They drink coffee in the green house.
  -> if <|[<|{drink: <|='coffee'>, colour: <|='green'>}>]>
  -- 4. The Dane drinks tea.
  -> if <|[<|{nationality: <|='Dane'>, drink: <|='tea'>}>]>
  -- zip with all permutations of pets
  -> templates
       current is $;
       $permutationsOf5... -> $pets($) -> $(.. as i; -> {$current($i), pet: $}) !
     end
  -- 3. The Swede has a dog.
  -> if <|[<|{nationality: <|='Swede'>, pet: <|='dog'>}>]>
  -- zip with all permutations of smokes
  -> templates
       current is $;
       $permutationsOf5... -> $smokes($) -> $(.. as i; -> {$current($i), smoke: $}) !
     end
  -- 7. The man who smokes Pall Mall has a bird.
  -> if <|[<|{smoke: <|='Pall Mall'>, pet: <|='bird'>}>]>
  -- 8. In the yellow house they smoke Dunhill.
  -> if <|[<|{colour: <|='yellow'>, smoke: <|='Dunhill'>}>]>
  -- 11. The Blend-smoker lives in the house next to the house with a cat.
  -> if <|[(<|{smoke: <|='Blend'>}>:<|{pet: <|='cat'>}>)] | [(<|{pet: <|='cat'>}>:<|{smoke: <|='Blend'>}>)]>
  -- 12. In a house next to the house with a horse, they smoke Dunhill.
  -> if <[(<{smoke: <='Dunhill'>}>:<{pet: <='horse'>}>)] | [(<{pet: <='horse'>}>:<{smoke: <='Dunhill'>}>)]>
  -- 13. The man who smokes Blue Master drinks beer.
  -> if <|[<|{smoke: <|='Blue Master'>, drink: <|='beer'>}>]>
  -- 14. The German smokes Prince.
  -> if <|[<|{smoke: <|='Prince'>, nationality: <|='German'>}>]>
  -- 16. They drink water in a house next to the house where they smoke Blend.
  -> if <|[(<|{smoke: <|='Blend'>}>:<|{drink: <|='water'>}>)] | [(<|{drink: <|='water'>}>:<|{smoke: <|='Blend'>}>)]>
  -> templates
       $... -> if <|{pet: <|='zebra'>}> -> 'The $(nationality:); owns the zebra.$#10;' !
       $(.. as i; -> '$i;: $;')... -> '$;$#10;' !
       '$#10;' !
      end !
